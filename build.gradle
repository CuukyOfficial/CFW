import org.apache.tools.ant.filters.ReplaceTokens
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group 'de.varoplugin'
version '0.6.7'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

sourceSets {
	main {
		java {
			srcDirs 'src'
		}

		resources {
			srcDirs 'resources'
		}
	}
}

repositories {
	mavenCentral()
	
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots'
	}
}
	
dependencies {
	compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
	compileOnly 'com.googlecode.json-simple:json-simple:1.1'
}

jar {
	if (project.hasProperty('destinationDir'))
		destinationDir = file(project.property('destinationDir').toString())

	if (project.hasProperty('fileName'))
		archiveFileName = project.property('fileName').toString()
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	from(sourceSets.main.resources.srcDirs) {
		filter ReplaceTokens, tokens: [name: 'CuukyFrameWork']
		filter ReplaceTokens, tokens: [version: project.version]
		filter ReplaceTokens, tokens: [author: 'Cuuky']
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			
			pom {
                name = 'CuukyFrameWork'
                description = 'A powerful bukkit framework that runs on 1.7+ and makes coding with bukkit less painful'
                url = 'https://github.com/CuukyOfficial/CFW'
			}
		}
	}

	repositories {
		maven {
			url 'https://archiva.varoplugin.de/repository/public'
			credentials {
                username project.hasProperty('repouser') ? project.property('repouser').toString() : null
                password project.hasProperty('repopassword') ? project.property('repopassword').toString() : null
            }
		}
	}
}
	